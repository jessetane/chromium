# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

component("tracing") {
  sources = [
    "child/child_memory_dump_manager_delegate_impl.cc",
    "child/child_memory_dump_manager_delegate_impl.h",
    "child/child_trace_message_filter.cc",
    "child/child_trace_message_filter.h",
    "common/graphics_memory_dump_provider_android.cc",
    "common/graphics_memory_dump_provider_android.h",
    "common/process_metrics_memory_dump_provider.cc",
    "common/process_metrics_memory_dump_provider.h",
    "common/tracing_messages.cc",
    "common/tracing_messages.h",
    "core/proto_utils.h",
    "core/proto_zero_message.cc",
    "core/proto_zero_message.h",
    "core/proto_zero_message_handle.cc",
    "core/proto_zero_message_handle.h",
    "core/scattered_stream_writer.cc",
    "core/scattered_stream_writer.h",
    "core/trace_ring_buffer.cc",
    "core/trace_ring_buffer.h",
    "tracing_export.h",
  ]

  defines = [ "TRACING_IMPLEMENTATION" ]

  deps = [
    "//base",
    "//ipc",
  ]

  if (is_nacl) {
    sources -= [ "common/process_metrics_memory_dump_provider.cc" ]
  }
}

component("startup_tracing") {
  sources = [
    "browser/trace_config_file.cc",
    "browser/trace_config_file.h",
    "common/trace_to_console.cc",
    "common/trace_to_console.h",
    "common/tracing_switches.cc",
    "common/tracing_switches.h",
    "tracing_export.h",
  ]

  defines = [ "TRACING_IMPLEMENTATION" ]

  deps = [
    "//base",
  ]
}

# For unit testing of generated stubs.
proto_library("proto_zero_testing_messages") {
  visibility = [ "//components/tracing/*" ]

  sources = [
    "test/example_messages.proto",
  ]

  generator_plugin_label = "tools/proto_zero_plugin:proto_zero_plugin"
  generator_plugin_suffix = ".pbzero"
  generate_cc = false
  generate_python = false

  # TODO(kraynov) Move away this complex path evaluation and dependency
  # injection to proto_library.gni. All lines below should be removed.
  # The problem is that root_out_dir and root_build_dir may differ in case of
  # cross compilation. Also .exe will be appended on Windows.

  plugin_host_label = generator_plugin_label + "($host_toolchain)"
  generator_plugin =
      rebase_path(get_label_info(plugin_host_label, "root_out_dir") + "/" +
                      get_label_info(plugin_host_label, "name"),
                  root_build_dir)
  if (is_win) {
    generator_plugin += ".exe"
  }

  deps = [
    plugin_host_label,
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "child/child_trace_message_filter_unittest.cc",
    "common/graphics_memory_dump_provider_android_unittest.cc",
    "common/process_metrics_memory_dump_provider_unittest.cc",
    "core/proto_utils_unittest.cc",
    "core/proto_zero_message_unittest.cc",
    "core/scattered_stream_writer_unittest.cc",
    "core/trace_ring_buffer_unittest.cc",
    "test/fake_scattered_buffer.cc",
    "test/proto_zero_generation_unittest.cc",
  ]

  deps = [
    ":proto_zero_testing_messages",
    ":tracing",
    "//base/test:test_support",
    "//ipc",
    "//testing/gmock:gmock",
    "//testing/gtest",
  ]

  if (!is_android) {
    sources += [ "browser/trace_config_file_unittest.cc" ]
    deps += [ ":startup_tracing" ]
  }
}
